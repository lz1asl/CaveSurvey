buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.j256.ormlite:ormlite-android:6.1'
    implementation 'org.apache.poi:poi:3.15'
    implementation 'org.apache.commons:commons-csv:1.9.0'

    // Bosch internal protocol
    implementation files('../CaveSurveyBoschGLM/binary/CaveSurveyBoschGLM.jar')

    // kotlin
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.10"
    implementation 'androidx.documentfile:documentfile:1.0.1'

    // unit tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    // container tests
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:3.4.0"
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            // We can leave these in environment variables
            storeFile file("CaveSurvey.keystore")
            keyAlias "CaveSurvey"

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword System.getenv("CaveSurveyKeystorePassword")
            keyPassword System.getenv("CaveSurveyKeyPassword")
        }
    }

    buildTypes {
        release {
            apply plugin: 'idea'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "defaultFlavor"

    productFlavors {
        defaultFlavor {
            proguardFile 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError false
    }

    testOptions {
        unitTests{
            all {
                useJUnitPlatform()
            }
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    buildToolsVersion '32.0.0'

}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

