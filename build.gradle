buildscript {
    ext.kotlin_version = '1.8.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.j256.ormlite:ormlite-android:6.1'
    implementation 'org.apache.poi:poi:3.15'
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation "androidx.multidex:multidex:2.0.1"

    // Bosch internal protocol
    implementation files('../CaveSurveyBoschGLM/binary/CaveSurveyBoschGLM.jar')

    // kotlin
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'androidx.documentfile:documentfile:1.0.1'

    // unit tests
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    // container tests
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:3.5.1"
}

android {

    namespace 'com.astoev.cave.survey'
    compileSdk 34

    defaultConfig {
        minSdk 14
        targetSdk 34
        versionCode 63
        versionName "2.10"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            // We can leave these in environment variables
            storeFile file("CaveSurvey.keystore")
            keyAlias "CaveSurvey"

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword System.getenv("CaveSurveyKeystorePassword")
            keyPassword System.getenv("CaveSurveyKeyPassword")
        }
    }

    buildTypes {
        release {
//            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    packaging {
        resources {
            excludes += ['LICENSE.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }


    testOptions {
        unitTests{
            all {
                useJUnitPlatform()
            }
            returnDefaultValues = true
            includeAndroidResources = true
            animationsDisabled = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    lint {
        abortOnError false
    }

}

kotlin {
    jvmToolchain 17
}
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

